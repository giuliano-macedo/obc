programa: lista_declaracoes
lista_declaracoes: lista_declaracoes declaracao 
	| declaracao
declaracao: declaracao_variaveis
	| declaracao_funcoes
declaracao_variaveis : tipo ID ";" 
    | tipo ID "[" NUM "]" ";"
tipo: "int"
    | "void"
declaracao_funcoes: tipo ID "(" parametros ")" declaracao_composta
parametros: lista_parametros
    | "void"
lista_parametros :lista_parametros "," param
    | param
param :  tipo ID
    | tipo ID "[" "]"
declaracao_composta: ( declaracao_locais lista_comandos )*
declaracao_locais: declaracao_locais declaracao_variaveis
    | 
lista_comandos: lista_comandos comando
    |
comando: declaracao_expressao
    | declaracao_composta 
    | declaracao_selecao 
    | declaracao_iteracao 
    | declaracao_retorno
declaracao_expressao: expressao ";" 
    | ";"
declaracao_selecao: "if" "(" expressao ")" comando 
    | "if" "(" expressao ")" comando "else" comando
declaracao_iteracao: "while" "(" expressao ")" comando
declaracao_retorno: "return" ";" 
    | "return" expressao ";"
expressao: variavel "=" expressao 
    | expressao_simples
variavel: ID 
    | ID "[" expressao "]"
expressao_simples: soma_expressao op_relacional soma_expressao 
    | soma_expressao
op_relacional: "<=" 
    | "<"
    | ">"
    | ">="
    | "=="
    | "!="
soma_expressao: soma_expressao soma termo 
    | termo
soma: "+" 
    | "-"
termo: termo mult fator 
    | fator
mult: "*" 
    | "/"
fator: "(" expressao ")" 
    | variavel 
    | ativacao 
    | NUM
ativacao: ID "(" argumentos ")"
argumentos: lista_argumentos 
    | 
lista_argumentos: lista_argumentos "," expressao 
    | expressao
